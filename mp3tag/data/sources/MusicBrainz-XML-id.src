# Mp3tag (v2.36+) parsing for musicbrainz.org XMLWebService 
# created by dano 2006-07-19
#
# This file should be in your sources dir. 
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#	
#	DO NOT COPY/MODIFY AND REDISTRIBUTE THIS WITHOUT PERMISSION
#

[Name]=Musicbrainz
[BasedOn]=http://musicbrainz.org
[IndexUrl]=http://musicbrainz.org/ws/1/release/
[AlbumUrl]=
[WordSeperator]=
[IndexFormat]=%_url%|%Album%|%Artist%|%Year%
[SearchBy]=%comment%
[Encoding]=utf-8

[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
debug "on" "c:\\debug_MusicBrainz-XML_A.html" "5"

sayoutput "CurrentUrl"
say "?type=xml&inc=tracks+artist+release-events"
say "|"
say "|"
say "|"
saynewline

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
#debug "on" "c:\\debug_MusicBrainz-XML-B.html"

# Album
outputto "Album"
findinline "<title>"
sayuntil "</title>"

findinline "<track-list>"
findinline "<artist id=" 1 1
movechar -11
if "<artist id="
	# ##############################################################
	# Varioos Artists
	gotoline 1
	
	# Year
	findinline "<event date=\"" 1 1
	movechar -13
	if "<event date=\""
		outputto "Year"
		findinline "<event date=\""
		saynextnumber
	else
		gotoline 1
	endif

	# Tracks
	findinline "<track-list>"	
	do	
		outputto "tracks"
		findinline "<title>"
		sayuntil "</title>"
		say "|"
		
		outputto "Artist"
		findinline "<name>"
		sayuntil "</name>"
		say "|"
		
		findinline "</track>"
	while "<track id"

	# End of VA
	# #############################################################
	
else
	gotoline 1
	
	# Artist
	outputto "Artist"
	findinline "<name>"
	sayuntil "</name>"

	# Year
	findinline "<event date=\"" 1 1
	movechar -13
	if "<event date=\""
		outputto "Year"
		findinline "<event date=\""
		saynextnumber
	else
		gotoline 1
	endif

	# Tracks
	findinline "<track-list>"
	outputto "tracks"
	do
		findinline "<title>"
		sayuntil "</title>"
		say "|"
	
		findinline "</track>"

	while "<track id"
endif
