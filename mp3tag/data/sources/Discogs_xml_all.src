[Name]=Discogs XML
[BasedOn]=http://www.discogs.com
[AlbumUrl]=http://www.discogs.com/release/%s?f=xml&api_key=1e48c7f4e4
[SearchBy]=%discogsid%
[Encoding]=utf-8

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################

#debug "on" "C:\debug.txt" 100

replace "|" ":vrln:"

replace "<anv />" ""
replace "<join />" ""
replace "<role />" ""
replace "<tracks />" ""

#fixes missing fields
findinline "</genres>"
ifnot "<styles>"
    gotochar 1
    RegexpReplace "</genres>" "</genres><styles></styles>"
endif

findinline "</styles>"
ifnot "<country>"
    gotochar 1
    RegexpReplace "</styles>" "</styles><country></country>"
endif

findinline "</country>"
ifnot "<released>"
    gotochar 1
    RegexpReplace "</country>" "</country><released></released>"
endif

# NOTES
outputto "NOTES"
gotochar 1
findinline "</released>"
if "<notes>"
    gotochar 1
    findinline "<notes>"
    sayuntilml "</notes>"
endif
gotoline 1
joinuntil "</resp>"

replace "|" ":vrln:"

replace "<anv />" ""
replace "<join />" ""
replace "<role />" ""
replace "<tracks />" ""
replace "<duration />" ""
RegexpReplace "<duration>.*?</duration>" ""

#fixes missing fields (again)
findinline "</genres>"
ifnot "<styles>"
    gotochar 1
    RegexpReplace "</genres>" "</genres><styles></styles>"
endif

findinline "</styles>"
ifnot "<country>"
    gotochar 1
    RegexpReplace "</styles>" "</styles><country></country>"
endif

findinline "</country>"
ifnot "<released>"
    gotochar 1
    RegexpReplace "</country>" "</country><released></released>"
endif

findinline "</released>"
ifnot "<notes>"
    gotochar 1
    RegexpReplace "</released>" "</released><notes></notes>"
endif

findinline "</notes>"
ifnot "<master_id>"
    gotochar 1
    RegexpReplace "</notes>" "</notes><master_id></master_id>"
endif

#fixes TRACK CD separator
regexpreplace "<position>(\d+?)[.: ](\d+?)</position>" "<position>\1-\2</position>"
replace "<position>CD" "<position>"
replace "<position>DVD" "<position>"
regexpreplace "<position>[-.:]" "<position>"

#fixes Written By
regexpreplace "Written[ -][Bb]y" "Written By"

# Box set fix
regexpreplace "(<format name=\"Box Set\"[^<]*?/>)(<format .*?</format>)" "\2\1"

# DISCOGSID
outputto "DISCOGSID"
findinline "id=\""
sayuntil "\""

# COVER
findinline "<artists>"
movechar -18
if "</images>"
    outputto "COVERURL"
    gotochar 1
    findinline  "uri=\""
    sayuntil "\""
    gotochar 1
    do
        findinline "type=\""
        if "primary"
            set "COVERURL" ""
            findinline  "uri=\""
            sayuntil "\""
            findinline "</images>"
            movechar -12
        endif
    findinline "/>"
    while "<image "
endif

# BAND
outputto "BANDTMP"
findinline "<artists>"
sayuntil "</artists>"
gotochar 1

# ALBUM
outputto "ALBUM"
findinline "<title>"
sayuntil "</title>"

# LABEL & CATALOG
outputto "PUBTMP"
findinline "<labels>"
sayuntil "</labels>"

# CREDITS
movechar 9
if "<extraartists>"
    movechar 14
    outputto "CREDTMP"
    sayuntil "</extraartists>"
endif

# MEDIATYPE, RELEASETYPE & QUANTINITY
outputto "MEDIATMP"
findinline "<formats>"
sayuntil "</formats>"


# GENRES
outputto "GENRE"
findinline "<genres>"
sayuntil "</genres>"

# STYLES
outputto "STYLE"
findinline "<styles>"
ifnot "</styles>"
    sayuntil "</styles>"
endif

# COUNTRY
outputto "COUNTRY"
findinline "<country>"
ifnot "</country>"
    sayuntil "</country>"
endif

# YEAR
outputto "YEARTMP"
findinline "<released>"
ifnot "</released>"
    sayuntil "</released>"
endif

# MASTERID
outputto "MASTERID"
findinline "<master_id>"
ifnot "</master_id>"
    sayuntil "</master_id>"
endif

# DISCNAME

findinline "<tracklist>"
do
    findinline "<track>"
    if "<position />"
        movechar 12
        if "<title>" 
            set "TMPDISCNAME" ""
            outputto "TMPDISCNAME"
            findinline "<title>"
            sayuntil "</title>"
        else
            set "TMPDISCNAME" ""
        endif
    else
        outputto "DISCNAME"
        sayoutput "TMPDISCNAME"
        say "|"
    endif
    findinline "</track>"
while "<track>"
set "TMPDISCNAME" ""
gotochar 1
RegexpReplace "<track><position />.*?</track>" ""

# TRACK fix
regexpreplace "<position>0(\d+?)</position>" "<position>\1</position>"
regexpreplace "<position>AA(\d*?)</position>" "<position>B\1</position>"
regexpreplace "<position>AAA(\d*?)</position>" "<position>C\1</position>"
regexpreplace "<position>AAAA(\d*?)</position>" "<position>D\1</position>"
regexpreplace "<position>X(\d*?)</position>" "<position>A\1</position>"
regexpreplace "<position>Y(\d*?)</position>" "<position>B\1</position>"
regexpreplace "<position>([ABCDEFGHIJKLMNOP])</position>(.*?)<position>\12</position>" "<position>\11</position>\2<position>\12</position>"

# TRACKNUMBERS & DISCNUMBERS
outputto "TRACKTMP"
findinline "<tracklist>"
do
    findinline "<position>"
    sayuntil "</position>"
    findinline "</track>"
    if "<track>"
        say "|"
    endif
while "<track>"
gotochar 1

# TOTALTRACKS
outputto "TOTALTMP"
findinline "<tracklist>"
do
    findinline "<position>"
    say ";"
    sayuntil "</position>"
    say ";"
    findinline "</track>"
while "<track>"
gotochar 1

# TITLE
outputto "TRACKS"
findinline "<tracklist>"
do
    findinline "<title>"
    sayuntil "</title>"
    findinline "</track>"
    if "<track>"
        say "|"
    endif
while "<track>"
gotochar 1

# ARTIST
outputto "ARTISTTMP"
findinline "<tracklist>"
do
    findinline "</title>"
    if "<artists>"
        outputto "ARTISTTMP"
        movechar 9
        sayuntil "</artists>"
        outputto "COMPILATION"
        set "COMPILATION" ""
        say "1"
        outputto "ARTISTTMP"
    else
        sayoutput "BANDTMP"
    endif
    findinline "</track>"
    if "<track>"
        say "|"
    endif
while "<track>"
gotochar 1

RegexpReplace "<artists>.*?</artists>" ""

# CREDITS per track
outputto "CRTMP"
findinline "<tracklist>"
do
    findinline "</title>"
    if "<extraartists>"
        movechar 14
        sayuntil "</extraartists>"
    else
        say " "
    endif
    findinline "</track>"
    if "<track>"
        say "|"
    endif
while "<track>"
gotochar 1

# END